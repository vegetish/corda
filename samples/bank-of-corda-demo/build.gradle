apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'net.corda.plugins.quasar-utils'
apply plugin: 'net.corda.plugins.publish-utils'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'maven-publish'

sourceSets {
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    // The bank of corda CorDapp depends upon Cash CorDapp features
    cordapp project(':finance')

    // Corda integration dependencies
    cordaCompile project(path: ":node:capsule", configuration: 'runtimeArtifacts')
    cordaCompile project(path: ":webserver:webcapsule", configuration: 'runtimeArtifacts')
    cordaCompile project(':core')
    cordaCompile project(':client:jfx')
    cordaCompile project(':client:rpc')
    cordaCompile project(':webserver')
    cordaCompile project(':node-driver')

    // Javax is required for webapis
    compile "org.glassfish.jersey.core:jersey-server:${jersey_version}"

    // Cordapp dependencies
    // Specify your cordapp's dependencies below, including dependent cordapps

    // Test dependencies
    testCompile "junit:junit:$junit_version"
}

task deployNodes(type: net.corda.plugins.Cordform, dependsOn: ['jar']) {
    directory "./build/nodes"
    // This name "Notary" is hard-coded into BankOfCordaClientApi so if you change it here, change it there too.
    // In this demo the node that runs a standalone notary also acts as the network map server.
    networkMap "O=Notary Service,L=Zurich,C=CH"
    node {
        name "O=Notary Service,L=Zurich,C=CH"
        notary = [validating : true]
        advertisedServices = []
        p2pPort 10002
        rpcPort 10003
        cordapps = ["net.corda:finance:$corda_release_version"]
    }
    node {
        name "O=BankOfCorda,L=London,C=GB"
        advertisedServices = ["corda.issuer.USD"]
        p2pPort 10005
        rpcPort 10006
        webPort 10007
        cordapps = ["net.corda:finance:$corda_release_version"]
        rpcUsers = [
                ['username'   : "bankUser",
                 'password'   : "test",
                 'permissions': ["StartFlow.net.corda.finance.flows.CashPaymentFlow",
                                 "StartFlow.net.corda.finance.flows.CashConfigDataFlow",
                                 "StartFlow.net.corda.finance.flows.CashExitFlow",
                                 "StartFlow.net.corda.finance.flows.CashIssueAndPaymentFlow",
                                 "StartFlow.net.corda.finance.flows.CashConfigDataFlow"]]
        ]
    }
    node {
        name "O=BigCorporation,L=New York,C=US"
        advertisedServices = []
        p2pPort 10008
        rpcPort 10009
        webPort 10010
        cordapps = ["net.corda:finance:$corda_release_version"]
        rpcUsers = [
                ['username'   : "bigCorpUser",
                 'password'   : "test",
                 'permissions': ["StartFlow.net.corda.finance.flows.CashPaymentFlow",
                                 "StartFlow.net.corda.finance.flows.CashConfigDataFlow"]]
        ]
    }
}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

idea {
    module {
        downloadJavadoc = true // defaults to false
        downloadSources = true
    }
}

publishing {
    publications {
        jarAndSources(MavenPublication) {
            from components.java
            artifactId 'bankofcorda'

            artifact sourceJar
            artifact javadocJar
        }
    }
}

task runIssuer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.bank.BankOfCordaDriverKt'
    args '--role'
    args 'ISSUER'
}

task runRPCCashIssue(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.bank.BankOfCordaDriverKt'
    args '--role'
    args 'ISSUE_CASH_RPC'
    args '--quantity'
    args 20000
    args '--currency'
    args 'USD'
}

task runWebCashIssue(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.bank.BankOfCordaDriverKt'
    args '--role'
    args 'ISSUE_CASH_WEB'
    args '--quantity'
    args 30000
    args '--currency'
    args 'GBP'
}

jar {
    manifest {
        attributes(
                'Automatic-Module-Name': 'net.corda.samples.demos.bankofcorda'
        )
    }
}
